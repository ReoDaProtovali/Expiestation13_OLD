/obj/structure/blob/special // Generic type for nodes/factories/cores/resource
	// Core and node vars: claiming, pulsing and expanding
	/// The radius inside which (previously dead) blob tiles are 'claimed' again by the pulsing overmind. Very rarely used.
	var/claim_range = 0
	/// The radius inside which blobs are pulsed by this overmind. Does stuff like expanding, making blob spores from factories, make resources from nodes etc.
	var/pulse_range = 0
	/// The radius up to which this special structure naturally grows normal blobs.
	var/expand_range = 0
	/// Should we start processing on init(and stop on del)
	var/should_process = FALSE

//COULD PROBABLY JUST CHECK THE STRAIN VALUES DIRECTLY
	// Area reinforcement vars: used by cores and nodes, for strains to modify
	/// Range this blob free upgrades to strong blobs at: for the core, and for strains
	var/strong_reinforce_range = 0
	/// Range this blob free upgrades to reflector blobs at: for the core, and for strains
	var/reflector_reinforce_range = 0
	/// The specific overmind this belongs to
	var/mob/eye/blob/owner

/obj/structure/blob/special/proc/reinforce_area(seconds_per_tick) // Used by cores and nodes to upgrade their surroundings
	//most strains only have shields
	var/higher_range_type = (strong_reinforce_range > reflector_reinforce_range) ? /obj/structure/blob/shield/core : /obj/structure/blob/shield/reflective/core
	var/lower_range_type
	var/lower_range = 0 //if this needs to be modularized then assoc lists could probably be used(very cursed)
	var/high_range = strong_reinforce_range || reflector_reinforce_range //if only 1 is set then this handles it
	if(strong_reinforce_range && reflector_reinforce_range)
		if(higher_range_type != /obj/structure/blob/shield/reflective/core)
			lower_range = reflector_reinforce_range
			lower_range_type = /obj/structure/blob/shield/reflective/core
		else
			higher_range_type = reflector_reinforce_range
			lower_range = strong_reinforce_range
			high_range = reflector_reinforce_range
			lower_range_type = /obj/structure/blob/shield/core

	if(high_range)
		for(var/obj/structure/blob/normal/tile in range(high_range, src))
			if(SPT_PROB(BLOB_REINFORCE_CHANCE, seconds_per_tick))
				var/selected_type = higher_range_type
				if(lower_range && get_dist(tile, src) <= lower_range)
					selected_type = lower_range_type
				tile.change_to(selected_type, owner)

/obj/structure/blob/special/proc/pulse_area(datum/team/blob/pulsing_team, claim_range = 10, pulse_range = 3, expand_range = 2)
	if(QDELETED(pulsing_team))
		pulsing_team = blob_team
	be_pulsed()
	var/expanded = FALSE
	if(prob(70*(1/BLOB_EXPAND_CHANCE_MULTIPLIER)) && expand())
		expanded = TRUE

	var/list/blobs_to_affect = list()
	for(var/obj/structure/blob/B in urange(claim_range, src, TRUE))
		blobs_to_affect += B //would a random insertion be faster?
	shuffle_inplace(blobs_to_affect)

	for(var/obj/structure/blob/affected in blobs_to_affect)
		if(!affected.blob_team)
			affected.blob_team = blob_team
			affected.update_appearance()
		var/distance = get_dist(get_turf(src), get_turf(affected))
		var/expand_probablity = max(20 - distance * 8, 1)
		if(affected.Adjacent(src))
			expand_probablity = 20
		if(distance <= expand_range)
			var/can_expand = TRUE
			if(blobs_to_affect.len >= 120 && !(COOLDOWN_FINISHED(affected, heal_timestamp)))
				can_expand = FALSE
			if(can_expand && COOLDOWN_FINISHED(affected, pulse_timestamp) && prob(expand_probablity*BLOB_EXPAND_CHANCE_MULTIPLIER))
				if(!expanded)
					var/obj/structure/blob/newB = affected.expand(null, null, !expanded) //expansion falls off with range but is faster near the blob causing the expansion
					if(newB)
						expanded = TRUE
		if(distance <= pulse_range)
			affected.be_pulsed()
